title: VillageChiefGreeting
tracking: always
---
<<declare $AliceMetCount = 0 as number>>
<<declare $BobMetCount = 0 as number>>
<<declare $CharlieMetCount = 0 as number>>
<<declare $DaveMetCount = 0 as number>>
<<declare $EllenMetCount = 0 as number>>
<<declare $FrankMetCount = 0 as number>>

<<declare $MissionNumber = 0 as number>>
<<declare $CompletedMissionNumber = 0 as number>>

<<if visited("VillageChiefGreeting") == false>>
    村長: {$PlayerName}、衛兵であるお主を呼んだのは他でもない、例の事件を解決してもらうためじゃ
    村長: 先日、近隣の村との交易記録が外部に流出した事件はお主も知っておるな？交易記録はわが村の状況が記録されている、重要な機密情報じゃ。
    村長: 同じことを起こさないためにも、お主にはこの事件の原因を突き止めてほしいのじゃ。そこで、まずは村の怪しい出来事の調査をお願いしたい！
    村長: わしには原因の検討もつかん...。どうか頼んだぞ！
<<elseif $MissionNumber == 1>>
    村長: 何が原因で情報が持ち出されたのか検討もつかん...。
    村長: この村の衛兵として、お主の力で事件を解決してくれ！
    村長: そこで、まずは村の怪しい出来事の調査をお願いしたい！
    村長: どうか頼んだぞ！
<<elseif $MissionNumber == 2>>
    村長: 調査、ご苦労であった。
    <<jump VillageChief1stQuestion>>
<<elseif $MissionNumber == 3>>
    村長: 調査、ご苦労であった。
    <<jump VillageChief2ndQuestion>>
<<elseif $MissionNumber == 4>>
    村長: 調査、ご苦労であった。
    <<jump VillageChief3rdQuestion>>
<<elseif $MissionNumber == 5>>
    村長: 調査、ご苦労であった。
    <<jump VillageChief4thQuestion>>
<<elseif $MissionNumber == 6>>
    村長: 調査、ご苦労であった。
    <<jump VillageChief5thQuestion>>
<<elseif $MissionNumber == 7>>
    村長: 調査、ご苦労であった。
    <<jump VillageChief6thQuestion>>
<<elseif $MissionNumber == 8>>
    村長: 誰が「帳簿管理の魔法の書」を持ち込んだのか、調査を続けてくれ！
<<else>>
    村長: 調査、ご苦労であった。
    <<jump VillageChief7thQuestion>>
<<endif>>

===
title: NarratorGreeting

---
ナレーター: この村では、村内で使用された呪文の痕跡をすべて記録している。この記録を事件解決に有効に活用しよう。
<<set $MissionNumber = 1>>
===
title: IntroSpellRecords

---
<<set $CompletedMissionNumber = 1>>
{$PlayerName}: これでだいたい聞き込みができたな...
{$PlayerName}: 呪文の痕跡も見て、何かわかったら村長に報告しよう！
<<set $MissionNumber = 2>>

===
title: VillageChiefQuestionInCorrect

---
ナレーター: もう少し呪文の痕跡を調べた方が良さそうだ...。
ナレーター: フランクに呪文の痕跡の調査のコツをもう一度聞いてみよう。
ナレーター: 村の左上にいたはずだ

===
title: VillageChief1stQuestion

---
村長: 何か怪しい出来事はあったかの?
    -> アリスが「帳簿管理の魔法の書」をメールで受け取った
        <<set $CompletedMissionNumber = 2>>
        村長: ふむふむ、そんなことがあったのか...。
        村長: その「帳簿管理の魔法の書」が何か怪しいことをしたんじゃろうか...。調査を続けてくれ!
        <<set $MissionNumber = 3>>
    -> ボブが「文書作成の魔道具」を起動した
        <<jump VillageChiefQuestionInCorrect>>
    -> チャーリーが「画像編集の魔道具」を起動した
        <<jump VillageChiefQuestionInCorrect>>
    -> デイヴが「webの秘伝書」を入手した
        <<jump VillageChiefQuestionInCorrect>>
    -> エレンが「月の満ち欠けの書物」を閲覧した
        <<jump VillageChiefQuestionInCorrect>>
    -> フランクが「記録の魔法の書」を展開した
        <<jump VillageChiefQuestionInCorrect>>
    -> もう少し調べる
        村長: 何かわかったら報告しておくれ！

===
title: VillageChief2ndQuestion

---
村長: 「帳簿管理の魔法の書」は何をしたんじゃ?
    -> 「芋煮の魔法の書」を作成した
        <<jump VillageChiefQuestionInCorrect>>
    -> 精霊を呼び出した
        <<set $CompletedMissionNumber = 3>>
        村長: そのような面妖な存在を呼び出したのか...。
        村長: その精霊とやらは何をしたんじゃろうか...。調査を続けてくれ!
        <<set $MissionNumber = 4>>
    -> 故郷の村にある日記を自宅の本棚に転送した
        <<jump VillageChiefQuestionInCorrect>>
    -> 「帳簿の魔道具」を起動した
        <<jump VillageChiefQuestionInCorrect>>
    -> もう少し調べる
        村長: 何かわかったら報告しておくれ！

===
title: VillageChief3rdQuestion

---
村長: 精霊は何をしたんじゃ？
    -> 村長へ手紙を送った
        <<jump VillageChiefQuestionInCorrect>>
    -> 魔力の使用状況を確認した
        <<jump VillageChiefQuestionInCorrect>>
    -> 暴走していた呪文を停止した
        <<jump VillageChiefQuestionInCorrect>>
    -> 複数の交易記録をどこかに送った
        <<set $CompletedMissionNumber = 4>>
        村長: 交易記録が流出させたのは精霊だったのか!
        村長: どのくらいの交易記録が流出したのか、続けて調べておくれ！
        <<set $MissionNumber = 5>>
    -> もう少し調べる
        村長: 何かわかったら報告しておくれ！

===
title: VillageChief4thQuestion

---
村長: 持ち出された交易記録の数は何個じゃ？
    -> 3個
        <<jump VillageChiefQuestionInCorrect>>
    -> 5個
        <<set $CompletedMissionNumber = 5>>
        村長: なんと...。そんなに多くの交易記録が流出してしまったのか...。
        村長: これは後が思いやられるの...
        村長: 今後同じようなことをなくすためにも、改めて交易記録が流出した原因はなんじゃったかの?
        <<set $MissionNumber = 6>>
    -> 7個
        <<jump VillageChiefQuestionInCorrect>>
    -> 9個
        <<jump VillageChiefQuestionInCorrect>>
    -> もう少し調べる
        村長: 何かわかったら報告しておくれ！

===
title: VillageChief5thQuestion

---
村長: 交易記録が流出した原因はなんじゃったかの？
    -> 「遠距離会話の魔道具」
        <<jump VillageChiefQuestionInCorrect>>
    -> 「browserの魔道具」
        <<jump VillageChiefQuestionInCorrect>>
    -> 「帳簿管理の魔法の書」
        <<set $CompletedMissionNumber = 6>>
        村長: そうじゃったな、アリスが持っていた「帳簿管理の魔法の書」が原因じゃったな。
        村長: この「帳簿管理の魔法の書」と精霊によって交易記録はどこに流出してしまったんじゃろうか...。
        村長: 引き続き調べてみてくれ!
        <<set $MissionNumber = 7>>
    -> 「人工知能の魔法の書」
        <<jump VillageChiefQuestionInCorrect>>
    -> もう少し調べる
        村長: 何かわかったら報告しておくれ！

===
title: VillageChief6thQuestion

---
村長: 交易記録はどこに送られてしまったんじゃろう？
    -> {$PlayerVillageName}村
        <<jump VillageChiefQuestionInCorrect>>
    -> {$AdversaryVillageName}村
        <<set $CompletedMissionNumber = 7>>
        村長: なんと...、犯人はもしかしたら{$AdversaryVillageName}村の人間かもしれないの...。
        村長: 誰が「帳簿管理の魔法の書」を持ち込んだのか、調査を続けてくれ！
        <<set $MissionNumber = 8>>
    -> デルタ村
        <<jump VillageChiefQuestionInCorrect>>
    -> エコー村
        <<jump VillageChiefQuestionInCorrect>>
    -> もう少し調べる
        村長: 何かわかったら報告しておくれ！

===
title: VillageChief7thQuestion

---
村長: 「帳簿管理の魔法の書」は誰から持ち込まれたんじゃ?
    -> {$AdversaryVillageName}村のイブ
        <<set $CompletedMissionNumber = 9>>
        村長: アリスがブラボー村からもらっていたものだったとは！他の村から持ち込まれた魔法の書には気をつけねばならんな。
        村長: これで犯人がイブであるということもわかった！礼を言うぞ！{$PlayerName}！
        村長: 皆の衆！ブラボー村のイブを捕まえるのじゃ！
        <<set $MissionNumber = 10>>
    -> {$AdversaryVillageName}村のマロリー
        <<jump VillageChief7thQuestionIncorrect>>
    -> {$PlayerVillageName}村のアリス
        <<jump VillageChief7thQuestionIncorrect>>
    -> {$PlayerVillageName}村のフランク
        <<jump VillageChief7thQuestionIncorrect>>
    -> もう少し調べる
        村長: 何かわかったら報告しておくれ！
===
title: VillageChief7thQuestionIncorrect

---
<<if $AliceMetCount == 1>>
    ナレーター: もう一度アリスの話を聞きにいこう。新しい情報を話してくれるかもしれない...。
<<else>>
    ナレーター: アリスの話を思い出してみよう。
<<endif>>
===
title: AliceGreeting

---

<<if $MissionNumber <= 7>>
    アリス: 大事な情報が流出するなんて、物騒なこともあるものね。
    アリス: 交易記録がある私の家の金庫は私か村長の呪文でしか開けられないようになっているから、セキュリティは万全のはずだったのに...。
    <<set $AliceMetCount = 1>>
<<else>>
    <<set $CompletedMissionNumber = 8>>
    アリス: まさか帳簿管理の魔法の書が原因だったなんて...。無害なものだと思って封印を解除してしまっていたわ。
    {$PlayerName}: 帳簿管理の魔法の書はどうやって手に入れたんだ？
    アリス: 転送呪文によってポストに送られてきたものよ。
    アリス: 送り主が書いてあるわ...
    アリス: 帳簿管理の魔法の書の送り主は...ブラボー村のイブよ。村長に知らせましょう。
    <<set $MissionNumber = 9>>
    <<set $AliceMetCount = 2>>
<<endif>>

===
title: BobGreeting

---
ボブ: 事件の調査か、ご苦労さん。
ボブ: そういえば、昨日の19時ごろにアリスの家から大きな物音がしていたな。普段は静かな家なんだが...
<<set $BobMetCount = 1>>

===
title: CharlieGreeting

---
チャーリー: この村では、今までこんな事件は起きた事がなかったんだよ。早く原因を知りたいね。
チャーリー: そういえば村長、昨日は丸1日遠くの町に出かけていたんだよ。村長のいない村は寂しかったなあ...
<<set $CharlieMetCount = 1>>

===
title: DaveGreeting

---
デイヴ: 村には囲うように結界が張ってあって呪文が通らないようになっているんだ。
デイヴ: 転送の魔法を使うときは、その呪文だけ通るように結界に穴を開けるんだよ。
<<set $DaveMetCount = 1>>

===
title: EllenGreeting

---
エレン: 呪文を自動で発動する魔法の書というものがあるらしい...
<<set $EllenMetCount = 1>>

===
title: FrankGreeting

---
フランク: 呪文の痕跡の調査の基本は「怪しいことがあった時間帯の記録を見る」と「わかった単語で検索する」だ!!
<<set $FrankMetCount = 1>>

===
