title: VillageChiefGreeting
tracking: always
---
<<declare $AliceMetCount = 0 as number>>
<<declare $BobMetCount = 0 as number>>
<<declare $CharlieMetCount = 0 as number>>
<<declare $DaveMetCount = 0 as number>>
<<declare $EllenMetCount = 0 as number>>
<<declare $FrankMetCount = 0 as number>>
<<declare $IsaacMetCount = 0 as number>>
<<declare $Correct1stQuestion = false as bool>>
<<declare $Correct2ndQuestion = false as bool>>
<<declare $Correct3rdQuestion = false as bool>>
<<declare $Correct4thQuestion = false as bool>>
<<declare $Correct5thQuestion = false as bool>>
<<declare $Correct6thQuestion = false as bool>>
<<declare $CorrectFinalQuestion = false as bool>>
<<if visited("VillageChiefGreeting") == false>>
    村長: {$PlayerName}、衛兵であるお主を呼んだのは他でもない、例の事件を解決してもらうためじゃ
    村長: 先日、近隣の村との交易記録が外部に流出した事件はお主も知っておるな？交易記録はわが村の状況が記録されている、重要な機密情報じゃ。
    村長: 同じことを起こさないためにも、お主にはこの事件の原因を突き止めてほしいのじゃ。そこで、まずは村の怪しい出来事の調査をお願いしたい！
    村長: わしには原因の検討もつかん...。どうか頼んだぞ！
<<elseif ($AliceMetCount == 0 or $BobMetCount == 0 or $CharlieMetCount == 0 or $DaveMetCount == 0 or $EllenMetCount == 0 or $FrankMetCount == 0)>>
    村長: 何が原因で情報が持ち出されたのか検討もつかん...。
    村長: この村の衛兵として、お主の力で事件を解決してくれ！
    村長: そこで、まずは村の怪しい出来事の調査をお願いしたい！
    村長: どうか頼んだぞ！
<<elseif $Correct1stQuestion == false>>
    村長: 調査、ご苦労であった。
    <<jump VillageChief1stQuestion>>
<<elseif $Correct2ndQuestion == false>>
    村長: 調査、ご苦労であった。
    <<jump VillageChief2ndQuestion>>
<<elseif $Correct3rdQuestion == false>>
    村長: 調査、ご苦労であった。
    <<jump VillageChief3rdQuestion>>
<<elseif $Correct4thQuestion == false>>
    村長: 調査、ご苦労であった。
    <<jump VillageChief4thQuestion>>
<<elseif $Correct5thQuestion == false>>
    村長: 調査、ご苦労であった。
    <<jump VillageChief5thQuestion>>
<<elseif $Correct6thQuestion == false>>
    村長: 調査、ご苦労であった。
    <<jump VillageChief6thQuestion>>
<<else>>
    村長: 調査、ご苦労であった。
    <<jump VillageChiefFinalQuestion>>
<<endif>>

===
title: NarratorGreeting

---
ナレーター: この村では、村内で使用された呪文の痕跡をすべて記録している。この記録を事件解決に有効に活用しよう。

===
title: IntroSpellRecords

---
{$PlayerName}: これでだいたい聞き込みができたな...
{$PlayerName}: 呪文の記録も見て、何かわかったら村長に報告しよう！

===
title: VillageChief1stQuestion

---
村長: 何か怪しい出来事はあったかの?
    -> アリスが「帳簿管理の魔法の書」をメールで受け取った
        村長: ふむふむ、そんなことがあったのか...。
        村長: その「帳簿管理の魔法の書」が何か怪しいことをしたんじゃろうか...。調査を続けてくれ!
        <<set $Correct1stQuestion = true>>
    -> ボブが「文書作成の魔道具」を起動した
        ナレーター: もう少し呪文の痕跡を調べた方が良さそうだ...。
    -> チャーリーが「画像編集の魔道具」を起動した
        ナレーター: もう少し呪文の痕跡を調べた方が良さそうだ...。
    -> デイヴが「webの秘伝書」を入手した
        ナレーター: もう少し呪文の痕跡を調べた方が良さそうだ...。
    -> エレンが「月の満ち欠けの書物」を閲覧した
        ナレーター: もう少し呪文の痕跡を調べた方が良さそうだ...。
    -> フランクが「記録の魔法の書」を展開した
        ナレーター: もう少し呪文の痕跡を調べた方が良さそうだ...。
    -> もう少し調べる
        村長: 何かわかったら報告しておくれ！

===
title: VillageChief2ndQuestion

---
村長: 「帳簿管理の魔法の書」は何をしたんじゃ?
    -> 複数の交易記録の書をどこかに送った
        ナレーター: もう少し呪文の痕跡を調べた方が良さそうだ...。
    -> 精霊を呼び出した
        村長: そのような面妖な存在を呼び出したのか...。
        村長: その精霊とやらは何をしたんじゃろうか...。調査を続けてくれ!
        <<set $Correct2ndQuestion = true>>
    -> 故郷の村にある日記を自宅の本棚に転送した
        ナレーター: もう少し呪文の痕跡を調べた方が良さそうだ...。
    -> 「帳簿の魔道具」を起動した
        ナレーター: もう少し呪文の痕跡を調べた方が良さそうだ...。
    -> もう少し調べる
        村長: 何かわかったら報告しておくれ！

===
title: VillageChief3rdQuestion

---
村長: 精霊は何をしたんじゃ？
    -> 村長へ手紙を送った
        ナレーター: もう少し呪文の痕跡を調べた方が良さそうだ...。
    -> 魔力の使用状況を確認した
        ナレーター: もう少し呪文の痕跡を調べた方が良さそうだ...。
    -> 暴走していた呪文を停止した
        ナレーター: もう少し呪文の痕跡を調べた方が良さそうだ...。
    -> 複数の交易記録をどこかに送った
        村長: 交易記録が流出させたのは精霊だったのか!
        村長: どのくらいの交易記録が流出したのか、続けて調べておくれ！
        <<set $Correct3rdQuestion = true>>
    -> もう少し調べる
        村長: 何かわかったら報告しておくれ！

===
title: VillageChief4thQuestion

---
村長: 持ち出された交易記録の数は何個じゃ？
    -> 3個
        ナレーター: もう少し呪文の痕跡を調べた方が良さそうだ...。
    -> 5個
        村長: なんと...。そんなに多くの交易記録が流出してしまったのか...。
        村長: これは後が思いやられるの...
        村長: 今後同じようなことをなくすためにも、改めて交易記録が流出した原因はなんじゃったかの?
        <<set $Correct4thQuestion = true>>
    -> 7個
        ナレーター: もう少し呪文の痕跡を調べた方が良さそうだ...。
    -> 9個
        ナレーター: もう少し呪文の痕跡を調べた方が良さそうだ...。
    -> もう少し調べる
        村長: 何かわかったら報告しておくれ！

===
title: VillageChief5thQuestion

---
村長: 交易記録が流出した原因はなんじゃったかの？
    -> 「遠距離会話の魔道具」
        ナレーター: もう少し呪文の痕跡を調べた方が良さそうだ...。
    -> 「browserの魔道具」
        ナレーター: もう少し呪文の痕跡を調べた方が良さそうだ...。
    -> 「帳簿管理の魔法の書」
        村長: そうじゃったな、アリスが持っていた「帳簿管理の魔法の書」が原因じゃったな。
        村長: この「帳簿管理の魔法の書」と精霊によって交易記録はどこに流出してしまったんじゃろうか...。
        村長: 引き続き調べてみてくれ!
        <<set $Correct5thQuestion = true>>
    -> 「人工知能の魔法の書」
        ナレーター: もう少し呪文の痕跡を調べた方が良さそうだ...。
    -> もう少し調べる
        村長: 何かわかったら報告しておくれ！

===
title: VillageChief6thQuestion

---
村長: 交易記録はどこに送られてしまったんじゃろう？
    -> {$PlayerVillageName}村
        ナレーター: もう少し呪文の痕跡を調べた方が良さそうだ...。
    -> {$AdversaryVillageName}村
        村長: なんと...、犯人はもしかしたら{$AdversaryVillageName}村の人間かもしれないの...。
        村長: 誰が「帳簿管理の魔法の書」を持ち込んだのか、調査を続けてくれ！
        <<set $Correct6thQuestion = true>>
    -> デルタ村
        ナレーター: もう少し呪文の痕跡を調べた方が良さそうだ...。
    -> エコー村
        ナレーター: もう少し呪文の痕跡を調べた方が良さそうだ...。
    -> もう少し調べる
        村長: 何かわかったら報告しておくれ！

===
title: VillageChiefFinalQuestion

---
村長: 「帳簿管理の魔法の書」は誰から持ち込まれたんじゃ?
    -> {$AdversaryVillageName}村のイブ
        村長: アリスがブラボー村からもらっていたものだったとは！他の村から持ち込まれた魔法の書には気をつけねばならんな。
        村長: これで犯人がイブであるということもわかった！礼を言うぞ！{$PlayerName}！
        村長: 皆の衆！ブラボー村のイブを捕まえるのじゃ！
        <<set $CorrectFinalQuestion = true>>
    -> {$AdversaryVillageName}村のマロリー
        <<jump VillageChiefFinalQuestionIncorrect>>
    -> {$PlayerVillageName}村のアリス
        <<jump VillageChiefFinalQuestionIncorrect>>
    -> {$PlayerVillageName}村のフランク
        <<jump VillageChiefFinalQuestionIncorrect>>
    -> もう少し調べる
        村長: 何かわかったら報告しておくれ！
===
title: VillageChief7thQuestionIncorrect

---
<<if $AliceMetCount == 1>>
    ナレーター: もう一度アリスの話を聞きにいこう。新しい情報を話してくれるかもしれない...。
<<else>>
    ナレーター: アリスの話を思い出してみよう。
<<endif>>
===
title: AliceGreeting

---

<<if $Correct6thQuestion == false>>
    アリス: 大事な情報が流出するなんて、物騒なこともあるものね。
    アリス: 交易記録がある私の家の金庫は私か村長の呪文でしか開けられないようになっているから、セキュリティは万全のはずだったのに...。
    <<set $AliceMetCount = 1>>
<<else>>
    アリス: まさか帳簿管理の魔法の書が原因だったなんて...。無害なものだと思って封印を解除してしまっていたわ。
    {$PlayerName}: 帳簿管理の魔法の書はどうやって手に入れたんだ？
    アリス: 転送呪文によってポストに送られてきたものよ。
    アリス: 送り主が書いてあるわ...
    アリス: 帳簿管理の魔法の書の送り主は...ブラボー村のイブよ。村長に知らせましょう。
    <<set $AliceMetCount = 2>>
<<endif>>

===
title: BobGreeting

---
ボブ: 事件の調査か、ご苦労さん。
ボブ: そういえば、昨日の19時ごろにアリスの家から大きな物音がしていたな。普段は静かな家なんだが...
<<set $BobMetCount = 1>>

===
title: CharlieGreeting

---
チャーリー: この村では、今までこんな事件は起きた事がなかったんだよ。早く原因を知りたいね。
チャーリー: そういえば村長、昨日は丸1日遠くの町に出かけていたんだよ。村長のいない村は寂しかったなあ...
<<set $CharlieMetCount = 1>>

===
title: DaveGreeting

---
デイヴ: 村には囲うように結界が張ってあって呪文が通らないようになっているんだ。
デイヴ: 転送の魔法を使うときは、その呪文だけ通るように結界に穴を開けるんだよ。
<<set $DaveMetCount = 1>>

===
title: EllenGreeting

---
エレン: 呪文を自動で発動する魔法の書というものがあるらしい...
<<set $EllenMetCount = 1>>

===
title: FrankGreeting

---
フランク: 呪文の記録の調査の基本は「怪しいことがあった時間帯の記録を見る」と「わかった単語で検索する」だ!!
<<set $FrankMetCount = 1>>

===
